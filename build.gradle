import org.gradle.internal.jvm.Jvm
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.0'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.9.20'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.7.1'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.27.0'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.vanniktech.maven.publish'

kotlin {
  androidNativeArm32()
  androidNativeArm64()
  androidNativeX86()
  androidNativeX64()
  iosArm64()
  iosX64()
  iosSimulatorArm64()
  js().nodejs()
  jvm().withJava() // Required for Animal Sniffer to work.
  linuxArm64()
  linuxX64()
  macosX64()
  macosArm64()
  mingwX64()
  tvosArm64()
  tvosX64()
  tvosSimulatorArm64()
  wasm().nodejs()
  watchosArm32()
  watchosArm64()
  watchosDeviceArm64()
  watchosX86()
  watchosX64()
  watchosSimulatorArm64()

  sourceSets {
    commonTest {
      dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-test'
      }
    }
  }
}

repositories {
  mavenCentral()
}

apply plugin: 'com.diffplug.spotless'
spotless {
  kotlin {
    ktlint().editorConfigOverride([
      'ktlint_standard_filename': 'disabled',
    ])
  }
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
    freeCompilerArgs += [
        '-progressive',
        '-Xjvm-default=all',
    ]
  }
}

tasks.withType(JavaCompile).configureEach {
  sourceCompatibility = JavaVersion.VERSION_1_8.toString()
  targetCompatibility = JavaVersion.VERSION_1_8.toString()
}

plugins.withType(NodeJsRootPlugin).configureEach {
  extensions.getByType(NodeJsRootExtension).nodeVersion = '20.0.0'
}

// Animal Sniffer only works on JDK 11 or older currently.
if (!Jvm.current().javaVersion.isJava12Compatible()) {
  apply plugin: 'ru.vyarus.animalsniffer'

  animalsniffer {
    sourceSets = [sourceSets.main] // Only check main sources, ignore test code.
  }
  dependencies {
    signature 'org.codehaus.mojo.signature:java18:1.0@signature'
    signature 'net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature'
  }
}
